plugins {
	id 'fabric-loom' version '0.4-SNAPSHOT'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'idea'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = 'suckeggs-fabric-1.16.1'
group = 'com.alcatrazescapee.suckeggs'

/**
 * Version in dev is set to 'dev'
 * Any builds produced by the github actions build script will have a version matching their tag.
 * All version numbers should be Semantic Versioning 2.0 compliant
 */
if (System.getenv('VERSION') != null) {
    version = System.getenv('VERSION')
} else {
    version = 'dev'
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

jar {
	from "LICENSE.md"
}

curseforge {
    def envApiKey = System.getenv('CURSEFORGE_API_KEY')
    if (envApiKey == null) {
        println 'Unable to locate api key in env variable'
        return
    }
    project {
        apiKey = envApiKey
        id = '319258'
        changelog = "[Release Notes ${version}](https://github.com/alcatrazEscapee/ore-veins/releases/tag/v${version}-fabric)"
        changelogType = "markdown"
        releaseType = 'beta'
        addGameVersion "${mc_version}"
    }
}